# Maintainer: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>
# Maintainer: Cody Schafer <aur@codyps.com>
# Contributor: Jake <aur@ja-ke.tech>

pkgname=dsview
pkgver=0.98
pkgrel=4
pkgdesc="Client software that supports the DreamSourceLab logic analyzer"
arch=('i686' 'x86_64')
url="http://www.dreamsourcelab.com/"
license=('GPL3')
depends=('boost-libs' 'qt5-base' 'libsigrokdecode4dsl' 'libsigrok4dsl' 'fftw')
makedepends=('boost')

source=("https://github.com/DreamSourceLab/DSView/archive/${pkgver}.tar.gz"
        'udev.rules'
        'dsview.desktop')
md5sums=('4515ede78609cb8aa4d65fce3ce76113'
         'af6d97ba0fbea628e320bdb4b469b766'
         'fa43a76bd84cbab060e1aeac1e9fa46c')

prepare() {
  cd "$srcdir/DSView-${pkgver}/DSView"
  sed -i 's#install(FILES icons/logo.png DESTINATION share/${PROJECT_NAME} RENAME logo.png)##; 
          s#install(FILES DreamSourceLab.rules DESTINATION /etc/udev/rules.d/)##; 
          s#install(FILES DSView.desktop DESTINATION /usr/share/applications/)##' CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_SKIP_RPATH=1 .
}

build() {
  cd "$srcdir/DSView-${pkgver}/DSView"
  cmake .
  make
}

package() {
  cd "$srcdir/DSView-${pkgver}/DSView"

  make DESTDIR="$pkgdir" PREFIX=/usr install
  
  mv "$pkgdir/usr/bin/DSView" "$pkgdir/usr/bin/${pkgname}"
  
  install -Dm644 "$srcdir/dsview.desktop" "$pkgdir/usr/share/applications/dsview.desktop"
  install -Dm644 "$srcdir/udev.rules" "$pkgdir/usr/lib/udev/rules.d/20-dsview.rules"
  
  for i in 16 32 48 64 128 256; do
    install -Dm644 icons/logo_${i}.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
  done
}

# vim:set ts=2 sw=2 et:
